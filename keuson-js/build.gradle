buildscript {
    ext.kotlin_version = '1.1.4-2'


    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'idea'

apply plugin: 'kotlin2js'
compileKotlin2Js {
    kotlinOptions.moduleKind = "commonjs"
    kotlinOptions.sourceMap = true
    kotlinOptions.metaInfo = true
    kotlinOptions.main = "noCall"
    kotlinOptions.outputFile = "$project.buildDir.path/classes/main/keuson.js"
}
compileTestKotlin2Js {
    kotlinOptions.moduleKind = "commonjs"
    kotlinOptions.sourceMap = true
    kotlinOptions.main = "noCall"
    kotlinOptions.outputFile = "$project.buildDir.path/classes/test/keuson-test.js"
}
sourceSets {
    main.kotlin.srcDirs += '../common'
}

apply plugin: 'maven'
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
artifacts {
    archives sourcesJar
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    compile 'com.github.kotlin-everywhere.kelibs-result:kelibs-result-js:d9866a420060f14a78b07a2df48da914d0750622'
    testCompile "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
}

build.doLast {
    (configurations.compile + configurations.testCompile).each { File file ->
        copy {
            includeEmptyDirs = false

            from zipTree(file.absolutePath)
            into "${buildDir}/kotlin-javascript-dependencies"
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith(".js") && (path.startsWith("META-INF/resources/") || !path.startsWith("META-INF/"))
            }
        }
    }
}

